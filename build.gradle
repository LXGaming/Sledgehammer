buildscript {
    repositories {
        jcenter()
        maven {
            name = "minecraftforge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "spongepowered"
            url = "https://repo.spongepowered.org/maven"
        }
    }

    dependencies {
        classpath("net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT")
        classpath("org.spongepowered:mixingradle:0.6-SNAPSHOT")
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "Sledgehammer"
version = "1.12.2-1.3.5"

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "stable_39"
    makeObfSourceJar = false
}

mixin {
    defaultObfuscationEnv = searge
    disableRefMapWarning = true
    add(sourceSets.main, "mixins.sledgehammer.refmap.json")
}

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    build.dependsOn("signJar")
}

repositories {
    jcenter()
    maven {
        name = "curseforge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    annotationProcessor("org.spongepowered:spongecommon:1.12.2-7.1.5:dev")
    provided("org.spongepowered:spongecommon:1.12.2-7.1.5:dev") {
        exclude(module: "commons-io")
        exclude(module: "gson")
        exclude(module: "guava")
        exclude(module: "launchwrapper")
    }
    provided("org.spongepowered:spongeforge:1.12.2-2768-7.1.5:dev")
    provided("org.spongepowered:spongevanilla:1.12.2-7.1.5:dev")
    provided("actually-additions:ActuallyAdditions:1.12.2:r145")
    provided("primalcore:PrimalCore:1.12.2:0.6.51")
    provided("quark:Quark:r1.5:145")
    provided("reliquary-v1-3:Reliquary:1.12.2:1.3.4.786")
}

jar {
    manifest {
        attributes(
                "FMLCorePlugin": "io.github.lxgaming.sledgehammer.launch.SledgehammerLoadingPlugin",
                "FMLCorePluginContainsFMLMod": true,
                "ForceLoadAsMod": true,
                "MixinConfigs": ""
                        + "mixins.sledgehammer.actuallyadditions.json,"
                        + "mixins.sledgehammer.core.json,"
                        + "mixins.sledgehammer.forge.json,"
                        + "mixins.sledgehammer.quark.json,"
                        + "mixins.sledgehammer.sponge.json,"
                        + "mixins.sledgehammer.xreliquary.json",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0
        )
    }

    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }

    exclude("dummyThing")
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)

    from (sourceSets.main.resources.srcDirs) {
        include("mcmod.info")
        expand("version": version, "mcversion": minecraft.version)
    }
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}