import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        maven {
            name = "lxgaming"
            url = "https://dl.bintray.com/lxgaming/maven"
        }
        maven {
            name = "minecraftforge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "spongepowered"
            url = "https://repo.spongepowered.org/maven"
        }
    }

    dependencies {
        classpath("com.github.jengelman.gradle.plugins:shadow:5.2.0")
        classpath("net.minecraftforge.gradle:ForgeGradle:2.3-20191227.215244-1")
        classpath("org.spongepowered:mixingradle:0.6-20191227.220440-1")
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "Sledgehammer"
version = "1.12.2-2.0.8"

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "stable_39"
    makeObfSourceJar = false
}

mixin {
    defaultObfuscationEnv = searge
    disableRefMapWarning = true
    add(sourceSets.main, "mixins.sledgehammer.refmap.json")
}

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    build.dependsOn("signJar")
    compileJava.dependsOn("filterJava")
    jar.classifier = "sponge"
}

repositories {
    jcenter()
    maven {
        name = "covers1624"
        url = "https://maven.covers1624.net/"
    }
    maven {
        name = "curseforge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
    maven {
        name = "lxgaming"
        url = "https://lxgaming.github.io/maven"
    }
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.5.2")
    shadow("org.spongepowered:configurate-hocon:3.6.1") {
        exclude(module: "guava")
    }
    shadow("org.spongepowered:mixin:0.8") {
        transitive = false
    }
    annotationProcessor("org.spongepowered:spongecommon:1.12.2-7.2.2-SNAPSHOT:dev")
    provided("org.spongepowered:spongecommon:1.12.2-7.2.2-SNAPSHOT:dev") {
        exclude(module: "configurate-core")
        exclude(module: "configurate-hocon")
        exclude(module: "commons-io")
        exclude(module: "gson")
        exclude(module: "guava")
        exclude(module: "launchwrapper")
        exclude(module: "mixin")
    }
    provided("org.spongepowered:spongeforge:1.12.2-2838-7.2.2-RC4024:dev") {
        transitive = false
    }
    provided("org.spongepowered:spongevanilla:1.12.2-7.2.2-RC355:dev") {
        transitive = false
    }

    // Third Party Mods
    provided("actually-additions:ActuallyAdditions:1.12.2:r145")
    provided("athenaeum:athenaeum:1.12.2:1.17.4")
    provided("armor-underwear-mod:armorunder:mc1.12.2:1.0.0")
    provided("astral-sorcery:astralsorcery:1.12.2:1.10.22")
    provided("barrels-drums-storage-more:BarrelsDrumsStorageAndMore:0.0.24")
    provided("biomes-o-plenty:BiomesOPlenty-1.12.2:7.0.1.2441:universal")
    provided("carrots-lib:carrotslib:mc1.12.2:1.0.0b1")
    provided("com.bewitchment:bewitchment:1.12.2-0.0.21.12")
    provided("champions:champions:1.12.2:1.0.11.3")
    provided("cyclops-core:CyclopsCore:1.12.2:1.6.6")
    provided("dank-null:DankNull:1.12.2:1.7.89")
    provided("ender-io:EnderIO:1.12.2:5.1.55")
    provided("ender-storage-1-8:EnderStorage-1.12.2:2.4.6.137:deobf")
    provided("flux-networks:fluxnetworks-1.12.2:3.0.19:21")
    provided("hearth-well:hwell:0.5.2")
    provided("immersive-engineering:ImmersiveEngineering:0.12:92")
    provided("integrated-dynamics:IntegratedDynamics:1.12.2:1.1.7")
    provided("journeymap:journeymap:1.12.2:5.7.0")
    provided("kubejs:KubeJS-forge:1.12.2:1.1.0.63")
    provided("logistics-pipes:logisticspipes:0.10.2.216")
    provided("matteroverdrive-legacy:MatterOverdrive-1.12.2:0.7.1.0:universal")
    provided("morph:Morph:1.12.2:7.1.3")
    provided("mowzies-mobs:mowziesmobs:1.5.8")
    provided("mrtjp:ProjectRed:1.12.2-4.9.3.116:mechanical") {
        exclude(module: "jei_1.12.2")
    }
    provided("natures-aura:NaturesAura:26.0")
    provided("primalcore:PrimalCore:1.12.2:0.6.105")
    provided("primitive-crafting:PrimitiveCrafting:1.12.2:1.4.0")
    provided("primitive-mobs:primitivemobs:1.2.3a")
    provided("pyrotech:pyrotech:1.12.2:1.3.13")
    provided("qmd:QMD:0.10.1:1.12.2")
    provided("quark:Quark:r1.5:145")
    provided("random-things:RandomThings:MC1.12.2:4.2.7.4")
    provided("real-filing-cabinet:realfilingcabinet:1.12:0.2.0.20")
    provided("ruins-structure-spawning-system:Ruins:1.12.2")
    provided("simple-storage-network:SimpleStorageNetwork:1.12.2:1.7.8")
    provided("sonar-core:sonarcore-1.12.2:5.0.19:20")
    provided("tomb-many-graves-2:TombManyGraves:1.12:4.2.0")
    provided("topography:Topography:1.12.2:1.5.1")
    provided("tschipp:carryon:1.12.1")
    provided("vaultopic-early-inventory-management:vaultopic:2.1")
}

artifacts {
    archives shadowJar
}

compileJava {
    doFirst {
        source = filterJava.outputs
    }
}

jar {
    manifest {
        attributes(
                "FMLCorePlugin": "io.github.lxgaming.sledgehammer.launch.SledgehammerLoadingPlugin",
                "FMLCorePluginContainsFMLMod": true,
                "ForceLoadAsMod": true,
                "MixinConfigs": ""
                        + "mixins.sledgehammer.actuallyadditions.json,"
                        + "mixins.sledgehammer.armorunder.json,"
                        + "mixins.sledgehammer.astralsorcery.json,"
                        + "mixins.sledgehammer.bdsandm.json,"
                        + "mixins.sledgehammer.bewitchment.json,"
                        + "mixins.sledgehammer.biomesoplenty.json,"
                        + "mixins.sledgehammer.carryon.json,"
                        + "mixins.sledgehammer.champions.json,"
                        + "mixins.sledgehammer.core.json,"
                        + "mixins.sledgehammer.danknull.json,"
                        + "mixins.sledgehammer.enderio.json,"
                        + "mixins.sledgehammer.enderstorage.json,"
                        + "mixins.sledgehammer.fluxnetworks.json,"
                        + "mixins.sledgehammer.forge.json,"
                        + "mixins.sledgehammer.immersiveengineering.json,"
                        + "mixins.sledgehammer.integrateddynamicscompat.json,"
                        + "mixins.sledgehammer.journeymap.json,"
                        + "mixins.sledgehammer.kubejs.json,"
                        + "mixins.sledgehammer.logisticspipes.json,"
                        + "mixins.sledgehammer.matteroverdrive.json,"
                        + "mixins.sledgehammer.morph.json,"
                        + "mixins.sledgehammer.mowziesmobs.json,"
                        + "mixins.sledgehammer.naturesaura.json,"
                        + "mixins.sledgehammer.platform.json,"
                        + "mixins.sledgehammer.primitivecrafting.json,"
                        + "mixins.sledgehammer.primitivemobs.json,"
                        + "mixins.sledgehammer.projectred.json,"
                        + "mixins.sledgehammer.pyrotech.json,"
                        + "mixins.sledgehammer.qmd.json,"
                        + "mixins.sledgehammer.quark.json,"
                        + "mixins.sledgehammer.randomthings.json,"
                        + "mixins.sledgehammer.realfilingcabinet.json,"
                        + "mixins.sledgehammer.ruins.json,"
                        + "mixins.sledgehammer.sponge.json,"
                        + "mixins.sledgehammer.storagenetwork.json,"
                        + "mixins.sledgehammer.tombmanygraves.json,"
                        + "mixins.sledgehammer.topography.json,"
                        + "mixins.sledgehammer.vaultopic.json,"
                        + "mixins.sledgehammer.wolforce.json",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0
        )
    }

    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }

    exclude("META-INF/*.DSA")
    exclude("META-INF/*.RSA")
    exclude("dummyThing")
    exclude("LICENSE.txt")
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-${archivesBaseName}")

    from (sourceSets.main.resources.srcDirs) {
        include("mcmod.info")
        expand("version": version, "mcversion": minecraft.version)
    }

    exclude("sledgehammer_at.cfg")
}

reobf {
    jar {
        mappingType = "SEARGE"
    }

    shadowJar {
        mappingType = "SEARGE"
    }

    thinShadowJar {
        mappingType = "SEARGE"
    }
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.shadow]
    relocate("com.typesafe.config", "io.github.lxgaming.sledgehammer.lib.config")
    relocate("ninja.leaping.configurate", "io.github.lxgaming.sledgehammer.lib.configurate")
    relocate("org.checkerframework", "io.github.lxgaming.sledgehammer.lib.checkerframework")
}

task thinShadowJar(type: ShadowJar) {
    archiveClassifier.set("thin")
    configurations = [project.configurations.shadow]
    relocators = shadowJar.relocators

    dependencies {
        exclude(dependency("org.spongepowered:mixin:0.8"))
    }

    manifest {
        inheritFrom jar.manifest
    }

    from sourceSets.main.output
}

artifacts {
    archives thinShadowJar
}

test {
    testLogging {
        exceptionFormat = "full"
    }

    useJUnitPlatform()
}

task filterJava(type: Sync) {
    from(sourceSets.main.java)
    into("$buildDir/tmp/$name")

    filter(ReplaceTokens, tokens: [version: version])
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}